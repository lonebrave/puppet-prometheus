# @summary This module manages prometheus node ipmi_exporter (https://github.com/soundcloud/ipmi_exporter)
# @param arch
#  Architecture (amd64 or i386)
# @param bin_dir
#  Directory where binaries are located
# @param collectors_enable
#  Collectors to enable, addtionally to the defaults
# @param collectors_disable
#  disable collectors which are enabled by default
# @param download_extension
#  Extension for the release binary archive
# @param download_url
#  Complete URL corresponding to the where the release binary archive can be downloaded
# @param download_url_base
#  Base URL for the binary archive
# @param extra_groups
#  Extra groups to add the binary user to
# @param extra_options
#  Extra options added to the startup command
# @param group
#  Group under which the binary is running
# @param init_style
#  Service startup scripts style (e.g. rc, upstart or systemd)
# @param install_method
#  Installation method: url or package (only url is supported currently)
# @param manage_group
#  Whether to create a group for or rely on external code for that
# @param manage_service
#  Should puppet manage the service? (default true)
# @param manage_user
#  Whether to create user or rely on external code for that
# @param os_type
#  Operating system (linux is the only one supported)
# @param package_ensure
#  If package, then use this for package ensure default 'latest'
# @param package_name
#  The binary package name - not available yet
# @param purge_config_dir
#  Purge config files no longer generated by Puppet
# @param restart_on_change
#  Should puppet restart the service on configuration change? (default true)
# @param service_enable
#  Whether to enable the service from puppet (default true)
# @param service_ensure
#  State ensured for the service (default 'running')
# @param service_name
#  Name of the node exporter service (default 'ipmi_exporter')
# @param user
#  User which runs the service
# @param version
#  The binary release version
class prometheus::ipmi_exporter (
  String[1] $package_name                 = 'ipmi_exporter',
  String $download_extension              = 'tar.gz',
  String[1] $version                      = 'v1.3.1',
  String[1] $package_ensure               = 'latest',
  String[1] $user                         = 'ipmi-exporter',
  String[1] $group                        = 'ipmi-exporter',
  Prometheus::Uri $download_url_base      = 'https://github.com/soundcloud/ipmi_exporter/releases',
  Array[String] $extra_groups             = [],
  Prometheus::Initstyle $init_style       = $facts['service_provider'],
  Boolean $purge_config_dir               = true,
  Boolean $restart_on_change              = true,
  Boolean $service_enable                 = true,
  Stdlib::Ensure::Service $service_ensure = 'running',
  String[1] $service_name                 = 'ipmi_exporter',
  Prometheus::Install $install_method     = $prometheus::install_method,
  Boolean $manage_group                   = true,
  Boolean $manage_service                 = true,
  Boolean $manage_user                    = true,
  String[1] $os_type                      = downcase($facts['kernel']),
  String $extra_options                   = '',
  Optional[Prometheus::Uri] $download_url = undef,
  String[1] $arch                         = $prometheus::real_arch,
  String[1] $bin_dir                      = $prometheus::bin_dir,
  Optional[Array[String]] $collectors     = undef,
  Array[String] $collectors_enable        = [],
  Array[String] $collectors_disable       = [],
  Optional[Stdlib::Host] $scrape_host     = undef,
  Boolean $export_scrape_job              = false,
  Stdlib::Port $scrape_port               = 9290,
  String[1] $scrape_job_name              = 'ipmi',
  Optional[Hash] $scrape_job_labels       = undef,
  Optional[String[1]] $bin_name           = undef,
  Boolean $unprivileged                   = true,
) inherits prometheus {
  package { 'freeipmi':
    ensure => 'present',
  }

  $real_download_url = pick($download_url,"${download_url_base}/download/${version}/${package_name}-${version}.${os_type}-${arch}.${download_extension}")

  $notify_service = $restart_on_change ? {
    true    => Service[$service_name],
    default => undef,
  }

  $cmd_collectors_enable = $collectors_enable.map |$collector| {
    "--collector.${collector}"
  }

  $cmd_collectors_disable = $collectors_disable.map |$collector| {
    "--no-collector.${collector}"
  }

  if $unprivileged {
    # crazy workaround from https://github.com/soundcloud/ipmi_exporter#running-as-unprivileged-user

    file { "/etc/sudoers.d/${service_name}":
      owner   => 'root',
      group   => 'root',
      mode    => '0644',
      content => 'ipmi-exporter ALL = NOPASSWD: /usr/sbin/ipmimonitoring,\
        /usr/sbin/ipmi-sensors,\
        /usr/sbin/ipmi-dcmi,\
        /usr/sbin/ipmi-raw,\
        /usr/sbin/bmc-info,\
        /usr/sbin/ipmi-chassis,\
        /usr/sbin/ipmi-sel
        ',
    }

    file { '/usr/local/bin/ipmi-sudo.sh':
      owner   => $user,
      group   => $group,
      mode    => '0750',
      content => '#!/bin/sh
        sudo /usr/sbin/$(basename $0) "$@"',
    }

    $sudo_rewrites = [
      'ipmimonitoring',
      'ipmi-sensors',
      'ipmi-dcmi',
      'bmc-info',
      'ipmi-chassis',
      'ipmi-raw',
    ]

    $sudo_rewrites.each |String $rewrite| {
      file { "/usr/local/bin/${rewrite}":
        ensure  => 'link',
        target  => '/usr/local/bin/ipmi-sudo.sh',
        require => File['/usr/local/bin/ipmi-sudo.sh'],
      }
    }

    $unprivileged_option = '--freeipmi.path=/usr/local/bin'
  }

  $options = join([
      $extra_options,
      join($cmd_collectors_enable, ' '),
      join($cmd_collectors_disable, ' '),
      $unprivileged_option,
  ], ' ')

  prometheus::daemon { $service_name:
    install_method     => $install_method,
    version            => $version,
    download_extension => $download_extension,
    os_type            => $os_type,
    arch               => $arch,
    real_download_url  => $real_download_url,
    bin_dir            => $bin_dir,
    notify_service     => $notify_service,
    package_name       => $package_name,
    package_ensure     => $package_ensure,
    manage_user        => $manage_user,
    user               => $user,
    extra_groups       => $extra_groups,
    group              => $group,
    manage_group       => $manage_group,
    purge              => $purge_config_dir,
    options            => $options,
    init_style         => $init_style,
    service_ensure     => $service_ensure,
    service_enable     => $service_enable,
    manage_service     => $manage_service,
    export_scrape_job  => $export_scrape_job,
    scrape_host        => $scrape_host,
    scrape_port        => $scrape_port,
    scrape_job_name    => $scrape_job_name,
    scrape_job_labels  => $scrape_job_labels,
    bin_name           => $bin_name,
    require            => Package['freeipmi'],
  }
}
